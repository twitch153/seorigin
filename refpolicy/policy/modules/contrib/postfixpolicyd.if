## <summary>Postfix policy server</summary>

########################################
## <summary>
##	All of the rules required to administrate 
##	an postfixpolicyd environment
## </summary>
## <param name="domain">
##	<summary>
##	Domain allowed access.
##	</summary>
## </param>
## <param name="role">
##	<summary>
##	The role to be allowed to manage the postfixpolicyd domain.
##	</summary>
## </param>
## <rolecap/>
#
interface(`postfixpolicyd_admin',`
	gen_require(`
		type postfix_policyd_t, postfix_policyd_conf_t;
		type postfix_policyd_var_run_t;
		type postfix_policyd_initrc_exec_t;	
	')

	allow $1 postfix_policyd_t:process { ptrace signal_perms };
	ps_process_pattern($1, postfix_policyd_t)

	init_labeled_script_domtrans($1, postfix_policyd_initrc_exec_t)
	domain_system_change_exemption($1)
	role_transition $2 postfix_policyd_initrc_exec_t system_r;
	allow $2 system_r;

	files_list_etc($1)
	admin_pattern($1, postfix_policyd_conf_t)

	files_list_pids($1)
	admin_pattern($1, postfix_policyd_var_run_t)
')
