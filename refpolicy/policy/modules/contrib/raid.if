## <summary>RAID array management tools</summary>

########################################
## <summary>
##	Execute software raid tools in the mdadm domain.
## </summary>
## <param name="domain">
##	<summary>
##	Domain allowed to transition.
##	</summary>
## </param>
#
interface(`raid_domtrans_mdadm',`
	gen_require(`
		type mdadm_t, mdadm_exec_t;
	')

	corecmd_search_bin($1)
	domtrans_pattern($1, mdadm_exec_t, mdadm_t)
')

######################################
## <summary>
##	Execute a domain transition to mdadm_t for the
##	specified role, allowing it to use the mdadm_t
##	domain
## </summary>
## <param name="role">
##	<summary>
##	Role allowed to access mdadm_t domain
##	</summary>
## </param>
## <param name="domain">
##	<summary>
##	Domain allowed to transition to mdadm_t
##	</summary>
## </param>
#
interface(`raid_run_mdadm',`
	gen_require(`
		type mdadm_t;
	')

	role $1 types mdadm_t;
	raid_domtrans_mdadm($2)
')

########################################
## <summary>
##	Create, read, write, and delete the mdadm pid files.
## </summary>
## <desc>
##	<p>
##	Create, read, write, and delete the mdadm pid files.
##	</p>
##	<p>
##	Added for use in the init module.
##	</p>
## </desc>
## <param name="domain">
##	<summary>
##	Domain allowed access.
##	</summary>
## </param>
#
interface(`raid_manage_mdadm_pid',`
	gen_require(`
		type mdadm_var_run_t;
	')

	# FIXME: maybe should have a type_transition.  not
	# clear what this is doing, from the original
	# mdadm policy
	allow $1 mdadm_var_run_t:file manage_file_perms;
')
