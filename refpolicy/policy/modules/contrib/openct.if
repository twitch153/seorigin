## <summary>Service for handling smart card readers.</summary>

########################################
## <summary>
##	Send openct a null signal.
## </summary>
## <param name="domain">
##	<summary>
##	Domain allowed access.
##	</summary>
## </param>
#
interface(`openct_signull',`
	gen_require(`
		type openct_t;
	')

	allow $1 openct_t:process signull;
')

########################################
## <summary>
##	Execute openct in the caller domain.
## </summary>
## <param name="domain">
## <summary>
##	Domain allowed access.
## </summary>
## </param>
#
interface(`openct_exec',`
	gen_require(`
		type openct_t, openct_exec_t;
	')

	corecmd_search_bin($1)
	can_exec($1, openct_exec_t)
')

########################################
## <summary>
##	Execute a domain transition to run openct.
## </summary>
## <param name="domain">
## <summary>
##	Domain allowed to transition.
## </summary>
## </param>
#
interface(`openct_domtrans',`
	gen_require(`
		type openct_t, openct_exec_t;
	')

	corecmd_search_bin($1)
	domtrans_pattern($1, openct_exec_t, openct_t)
')

########################################
## <summary>
##	Read openct PID files.
## </summary>
## <param name="domain">
##	<summary>
##	Domain allowed access.
##	</summary>
## </param>
#
interface(`openct_read_pid_files',`
	gen_require(`
		type openct_var_run_t;
	')

	files_search_pids($1)
	read_files_pattern($1, openct_var_run_t, openct_var_run_t)
')

########################################
## <summary>
##	Connect to openct over an unix stream socket.
## </summary>
## <param name="domain">
##	<summary>
##	Domain allowed access.
##	</summary>
## </param>
#
interface(`openct_stream_connect',`
	gen_require(`
		type openct_t, openct_var_run_t;
	')

	files_search_pids($1)
	stream_connect_pattern($1, openct_var_run_t, openct_var_run_t, openct_t)
')
