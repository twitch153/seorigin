## <summary>hddtemp hard disk temperature tool running as a daemon.</summary>

#######################################
## <summary>
##	Execute a domain transition to run hddtemp.
## </summary>
## <param name="domain">
##	<summary>
##	Domain allowed to transition.
##	</summary>
## </param>
#
interface(`hddtemp_domtrans',`
	gen_require(`
		type hddtemp_t, hddtemp_exec_t;
	')

	domtrans_pattern($1, hddtemp_exec_t, hddtemp_t)
	corecmd_search_bin($1)
')

######################################
## <summary>
##	Execute hddtemp.
## </summary>
## <param name="domain">
##	<summary>
##	Domain allowed access.
##	</summary>
## </param>
#
interface(`hddtemp_exec',`
	gen_require(`
		type hddtemp_exec_t;
	')

	can_exec($1, hddtemp_exec_t)
	corecmd_search_bin($1)
')

########################################
## <summary>
##	All of the rules required to
##	administrate an hddtemp environment.
## </summary>
## <param name="domain">
##	<summary>
##	Domain allowed access.
##	</summary>
## </param>
## <param name="role">
##	<summary>
##	Role allowed access.
##	</summary>
## </param>
## <rolecap/>
#
interface(`hddtemp_admin',`
	gen_require(`
		type hddtemp_t, hddtemp_etc_t, hddtemp_initrc_exec_t;
	')

	allow $1 hddtemp_t:process { ptrace signal_perms };
	ps_process_pattern($1, hddtemp_t)

	init_labeled_script_domtrans($1, hddtemp_initrc_exec_t)
	domain_system_change_exemption($1)
	role_transition $2 hddtemp_initrc_exec_t system_r;
	allow $2 system_r;

	admin_pattern($1, hddtemp_etc_t)
	files_search_etc($1)

	allow $1 hddtemp_t:dir list_dir_perms;
	read_lnk_files_pattern($1, hddtemp_t, hddtemp_t)
	kernel_search_proc($1)
')
